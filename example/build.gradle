plugins {
    id("com.android.application")
    id("com.google.gms.google-services")
}

dependencies {

    // Projects
    implementation project(':rx-android-extensions')
    implementation project(':example-model')

    // Android
    implementation 'androidx.multidex:multidex:2.0.0'
    implementation 'androidx.appcompat:appcompat:1.0.0'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.palette:palette:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.0.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'com.google.code.findbugs:jsr305:3.0.2'

    // Recycler view helpers
    implementation 'com.github.jacek-marchwicki.recyclerview-changes-detector:universal-adapter:2.0.1'
    implementation 'com.github.jacek-marchwicki.recyclerview-changes-detector:universal-adapter-rx:2.0.1'

    // Firebase
    implementation 'com.firebaseui:firebase-ui-auth:7.1.1'

    implementation "com.github.kazik117.RxBinding:rxbinding:migrate-androidx-SNAPSHOT"
    implementation "com.github.kazik117.RxBinding:rxbinding-support-v4:migrate-androidx-SNAPSHOT"
    implementation "com.github.kazik117.RxBinding:rxbinding-appcompat-v7:migrate-androidx-SNAPSHOT"
    implementation "com.github.kazik117.RxBinding:rxbinding-recyclerview-v7:migrate-androidx-SNAPSHOT"
}

Properties props = new Properties()
def keystoreProperties = file("keystore.properties")
if (keystoreProperties.exists()) {
    props.load(new FileInputStream(keystoreProperties))
}

android {
    compileSdkVersion 28

    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'LICENSE.txt'
        exclude 'NOTICE.txt'
    }

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 28
        versionCode 1
        versionName "0.0.1"
        applicationId "com.appunite.rx.example"
        multiDexEnabled = true
        testHandleProfiling true
        testFunctionalTest true
    }

    signingConfigs {
        debug {
            storeFile file("appunite-debug.keystore")
        }
        release {
            storeFile file("${props.getProperty('example.key.store', 'appunite-debug.keystore')}")
            storePassword "${props.getProperty('example.key.store.password', 'android')}"
            keyAlias "${props.getProperty('example.key.alias', 'androiddebugkey')}"
            keyPassword "${props.getProperty('example.key.alias.password', 'android')}"
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            debuggable false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            signingConfig signingConfigs.release
        }
        debug {
            debuggable true
            minifyEnabled false
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
        }
    }

    lintOptions {
        abortOnError true
        disable 'InvalidPackage'
        xmlOutput file("${reportsDir}/lint/lint-results.xml")
        htmlOutput file("${reportsDir}/lint/lint-results.html")
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

}